plugins {
    id 'org.springframework.boot' version '2.4.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id "io.freefair.lombok" version "5.3.0"
    id "com.github.ben-manes.versions" version "0.38.0"
}

group = 'no.fint'
sourceCompatibility = '11'

repositories {
    mavenLocal()
    maven {
        url 'https://repo.fintlabs.no/releases'
    }
    mavenCentral()
}

dependencies {
    implementation('no.fintlabs:fint-audit-api:2.2.0')

    implementation('com.azure:azure-messaging-eventhubs:5.5.0')
    implementation('com.azure:azure-messaging-eventhubs-checkpointstore-blob:1.5.1')

    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    implementation('io.projectreactor:reactor-core:3.4.4')
    implementation('org.apache.commons:commons-lang3:3.12.0')
    implementation('com.google.guava:guava:30.1.1-jre')
}

task copyExternalDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/deps/external"
    exclude '**/fint-*.jar'
}

task copyFintDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/deps/fint"
    include '**/fint-*.jar'
}

build.dependsOn(copyExternalDependencies)
build.dependsOn(copyFintDependencies)

jar {
    enabled(true)
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Main-Class": "no.fint.audit.web.Application",
                "Specification-Version": "${version}",
                "Implementation-Version": "${version}",
                "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' ')
        )
    }
}

bootJar {
    enabled(false)
}
