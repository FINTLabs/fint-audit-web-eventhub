plugins {
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id "io.freefair.lombok" version "5.0.0-rc6"
    id "com.github.ben-manes.versions" version "0.28.0"
}

group = 'no.fint'
sourceCompatibility = '1.8'

repositories {
    mavenLocal()
    jcenter()
    maven {
        url "http://dl.bintray.com/fint/maven"
    }
}

dependencies {
    implementation('no.fint:fint-audit-api:1.2.1')
    implementation('no.fint:fint-spring-secrets:0.1.0')

    implementation('com.azure:azure-messaging-eventhubs:5.0.2')

    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    implementation('io.projectreactor:reactor-core:3.3.3.RELEASE')
    implementation('org.apache.commons:commons-lang3:3.9')
    implementation('com.google.guava:guava:28.2-jre')
}

task copyExternalDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/deps/external"
    exclude '**/fint-*.jar'
}

task copyFintDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/deps/fint"
    include '**/fint-*.jar'
}

build.dependsOn(copyExternalDependencies)
build.dependsOn(copyFintDependencies)

jar {
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Main-Class": "no.fint.audit.web.Application",
                "Specification-Version": "${version}",
                "Implementation-Version": "${version}",
                "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' ')
        )
    }
}

bootJar {
    enabled(false)
}
